plugins {
    id 'common'
    id 'xyz.wagyourtail.unimined'
}

unimined.minecraft(sourceSets.main, true) {
    version gradle.ext.minecraft_version

    mappings {
        mojmap()
        devNamespace "mojmap"
        devFallbackNamespace "mojmap"
    }
}

if (project.name == "common") return // Don't apply the code below to the common project

def minecraftConfig = configurations.findByName("minecraft")
if (minecraftConfig != null) {
    configurations.compileClasspath.extendsFrom(minecraftConfig)
    configurations.runtimeClasspath.extendsFrom(minecraftConfig)
}

unimined.minecraft(sourceSets.main, true) {
    mods.modImplementation {
        mixinRemap {
            reset()
            enableBaseMixin()
            enableMixinExtra()
        }
    }
}

dependencies {
    implementation(project(":common"))
}

processResources {
    from project(":common").sourceSets.main.resources
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
